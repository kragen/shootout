\ $Id: ackermann.gforth,v 1.2 2004-12-05 01:59:48 bfulgham Exp $
\ ackermann's function
\ http://www.bagley.org/~doug/shootout/
\ from Andrew Pahi
\ enhanced by mmihai@delajii.net

\ if x = 0 		A(x,y) = y + 1
\ if y = 0		A(x,y) = A(x-1,1)
\ A(x,y) = A(x-1, A(x, y-1))

\ read NUM from last command line argument
0. argc @ 1- arg >number 2drop drop constant NUM

: -ack ( y x -- n )
   ?dup if
     swap ?dup
      if
         1- over recurse swap 1- recurse
      else
         1- 1 swap recurse
      then
   else
      1+
   then ;

\ run ack(3, NUM) and print result from stack
." Ack(3," NUM 1 u.r ." ): " NUM 3 -ack 1 u.r cr

bye \ th-th-that's all folks!
