\ $Id: heapsort.gforth,v 1.1 2004-05-25 03:06:29 bfulgham Exp $
\ http://www.bagley.org/~doug/shootout/
\ from Anton Ertl

0. argc @ 1- arg >number 2drop drop constant NUM

139968 constant IM
  3877 constant IA 
 29573 constant IC 

variable SEED
42 SEED !

: gen_random ( r -- r )
  IA SEED @ * IC + IM mod dup SEED ! s>d d>f
  f* [ IM s>d d>f ] fliteral f/ ;

: heap-sort ( ra n -- )
    swap { ra }
    dup 2/ 1+ begin ( ir l )
	dup 1 > if  ( ir l )
	    1- dup floats ra + f@ ( ir l rra )
	else
	    over floats ra + dup >r f@ ( ir l rra R: ra+ir )
	    1 floats ra + f@ r> f! ( ir l rra )
	    swap 1- dup 1 = if ( l ir rra )
		1 floats ra + f!
		2drop exit
	    endif
	    swap endif ( ir l rra )
	{ ir l } ( rra )
	l l 2* begin ( i j rra )
	    dup ir <=
	while ( i j rra )
	    dup ir < if
		dup floats ra + dup f@ float+ f@ f< if
		    1+
		endif
	    endif
	    dup floats ra + f@ fover fover f< if ( i j rra ra[j] )
		over floats ra + f!
		nip dup 2*
	    else
		fdrop drop ir 1+
	    endif
	repeat
	drop floats ra + f!
	ir l
    again ;

: my-f. ( r -- )
    \ prints r for 0.1<=r<1
    ." 0." pad 10 2dup represent 2drop drop type ;

: main ( -- )
    NUM 1+ floats allocate throw ( ra )
    dup NUM floats bounds ?do
	1e gen_random i f!
	1 floats +loop
    ( ra ) dup NUM heap-sort
    NUM floats + f@ my-f. cr ;

main bye
