(*
 * $Id: wordfreq.ocaml,v 1.3 2004-09-29 06:24:32 bfulgham Exp $
 * http://shootout.alioth.debian.org/
 * with help from Markus Mottl
 *)

let max = 4096
let buf = String.create max
let count = Hashtbl.create 75000 and wbuf = Buffer.create 64
let to_list w c l = Printf.sprintf "%7d %s" !c w :: l

let rec scan_words i n inword =
  if i < n then
    let c = buf.[i] in
    if 'a' <= c && c <= 'z' then begin
      Buffer.add_char wbuf c;
      scan_words (i + 1) n true
    end
    else if 'A' <= c && c <= 'Z' then begin
      Buffer.add_char wbuf (Char.unsafe_chr (Char.code c + 32));
      scan_words (i + 1) n true
    end
    else if inword then begin
      let word = Buffer.contents wbuf in
      begin try incr (Hashtbl.find count word)
      with Not_found -> Hashtbl.add count word (ref 1) end;
      Buffer.clear wbuf;
      scan_words (i + 1) n false
    end
    else scan_words (i + 1) n false
  else
    let nread = input stdin buf 0 max in
    if nread <> 0 then scan_words 0 nread inword;;

scan_words 0 0 false;

let out_lines = Hashtbl.fold (fun k d l -> (k, !d) :: l) count [] in
List.iter (fun (k, d) -> Printf.printf "%7d %s\n" d k)
  (List.sort (fun (ka, da) (kb, db) -> 
                let c = compare db da in
                if c <> 0 then c
                else String.compare kb ka) out_lines)

