\ -*- mode: forth -*-
\ $Id: random.gforth,v 1.1 2004-05-25 03:28:09 bfulgham Exp $
\ http://www.bagley.org/~doug/shootout/
\ from Jorge Acereda Maciá

0. argc @ 1- arg >number 2drop drop constant NUM

: FIM 1e 139968e f/ POSTPONE FLITERAL ; immediate
: IA 3877 POSTPONE LITERAL ; immediate
: IC 29573 POSTPONE LITERAL ; immediate
: IM 139968 POSTPONE LITERAL ; immediate
42 value seed

\ n = the uth power of n1
: pow ( n1 u -- n )
  1 swap 0 u+do over * loop nip ;

\ print float r with n fixed decimal points
: ffix ( r n -- )
  dup 10 swap pow 0 d>f f* 0.5e0 f+ f>d
  <<# rot 0 ?do # loop [char] . hold #s #> type #>> ;

: gen-random 
    s" seed IA * IC + IM mod dup to seed " evaluate
    s" 0 d>f f* FIM f* " evaluate ; immediate 

: main
    10 SET-PRECISION
    0e NUM 0 do fdrop 100e gen-random loop 9 ffix cr ;

main bye
