(*
** The Computer Language Benchmarks Game
** http://shootout.alioth.debian.org/
**
** contributed by Hongwei Xi
**
** compilation command:
**   atscc -O3 binary-trees2.dats -o binary-trees2 -D_ATS_GCATS
*)

(* ****** ****** *)

staload _(*anonymous*) = "prelude/DATS/reference.dats"

(* ****** ****** *)

dataviewtype tree (int) =
  Nil(0) | {n1,n2:two} Node(1) of (tree n1, int, tree n2)
// end of [tree]

viewtypedef Tree = [n:two] tree n

fun tree_make (d: int, i: int): Tree =
  if d > 0 then let
    val d1 = d-1 and i2 = i << 1 in
    Node (tree_make (d1, i2 - 1), i, tree_make (d1, i2))
  end else Node (Nil (), i, Nil ())
// end of [tree_make]

fun check_and_free (t: Tree):<!ntm> int =  case+ t of
  | ~Node (tl, i, tr) => i + check_and_free tl - check_and_free tr
  | ~Nil () => 0
// end of [check_and_free]

fun check (t: !Tree):<!ntm> int = case+ t of
  | Node (!tl, i, !tr) => let
      val ans = i + check (!tl) - check (!tr) in (fold@ t; ans)
    end // end of [Node]
  | Nil () => (fold@ t; 0)
// end of [check]

fun check_ref (r: ref Tree): int = let
  val (vbox pf | p) = ref_get_view_ptr r in check !p     
end // end of [check_ref]

(* ****** ****** *)

#define MIN_DEPTH 4

fn stretch (max_depth: Nat): void = let
   val stretch_depth = max_depth + 1
   val t = tree_make (stretch_depth, 0)
   val c = check_and_free (t)
in
   printf ("stretch tree of depth %i\t check: %i\n", @(stretch_depth, c));
end // end of [stretch]

fn long_lived_tree_make
  (max_depth: Nat): ref Tree = let
  val t = tree_make (max_depth, 0); val t_r = ref<Tree> (t)
in
  t_r
end // end of [long_lived_tree_make]

fun loop_depths (d: Nat, max_depth: Nat): void = begin
  if d <= max_depth then let
    val n = 1 << (max_depth - d + MIN_DEPTH)
    fun loop (i: Nat, c: int):<cloref1> int =
      if i < n then let
        val t = tree_make(d,  i); val c1 = check_and_free t
        val t = tree_make(d, ~i); val c2 = check_and_free t
      in
        loop (i+1, c + c1 + c2)
      end else c
    val c = loop (0, 0)
  in
    printf ("%i\t trees of depth %i\t check: %i\n", @(2 * n, d, c));
    loop_depths (d + 2, max_depth)
  end
end // end of [loop_depths]

implement main (argc, argv) = let
  val () = assert_errmsg
    (argc = 2, "Exit: wrong command format!\n")
  val n = int1_of argv.[1]
  val () = assert_errmsg
    (n >= 0, "The input integer needs to be a natural number.\n")
  // this simply means that no garbage collection can be triggered
  // and memory allocation and deallocation must be handled manually
  val () = gc_chunk_count_limit_max_set (~1) // infinite
  val max_depth = max (MIN_DEPTH + 2, n)
  val () = stretch (max_depth)
  val long_lived_tree = long_lived_tree_make (max_depth)
in
  loop_depths (MIN_DEPTH, max_depth);
  printf ("long lived tree of depth %i\t check: %i\n", @(max_depth, check_ref long_lived_tree))
end // end of [main]

(* ****** ****** *)

(* end of [binary-tree2.dats] *)
