# -*- mode: icon -*-
# $Id: regexmatch.icon,v 1.1 2004-05-23 07:14:28 bfulgham Exp $
# http://www.bagley.org/~doug/shootout/
# from Cheyenne Wills

procedure main(argv)
    n := \argv[1] | 1

    lines := []
    while put(lines,read())

    counter := 0

    start := '(' ++ &digits        # Define what starts a phone number

    every i := 1 to n do {
        every line := !lines do {
            line ? {
                tab(upto(start))   # Find a possible start...
                    (

                     ## Areacode
                     (( ="(" & integer(areacode := move(3)) & =")" ) |
                      (        integer(areacode := move(3)) )) &
                     =" " &                         # followed by a space
                     ## Exchange
                     integer(exchange := move(3)) & # followed by 3 digits
                     =(" " | "-") &                 # followed by space or dash
                     ## Number
                     integer(num := move(4)) &      # followed by 4 digits

                     ( not any(&digits) | pos(0))   # followed by any non digit
                                                    # or end of line
                     ) &
                         if i = n then              # Last time through??
                             write(counter +:= 1,   # then write the output
                                   ": (",areacode,") ",exchange,"-",num)
            }
        }
    }
end
