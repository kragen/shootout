-- $Id: matrix.gnat,v 1.2 2005-03-13 23:29:08 bfulgham Exp $
-- http://shootout.alioth.debian.org/
--
-- Contributed by Pascal Obry.

with Text_IO, Ada.Strings.Fixed, Ada.Command_Line;

procedure Matrix is

   pragma Suppress (All_Checks);

   use Ada;

   Size : constant Natural := 30;

   type Int is new Integer;
   type Int_Matrix is array (1 .. Size, 1 .. Size) of Int;

   function L_Trim (Source : String; Side : Ada.Strings.Trim_End :=
       Ada.Strings.Left ) return String renames Ada.Strings.Fixed.Trim;

   procedure Mk_Matrix (M : out Int_Matrix) is
      Count : Int := 1;
   begin
      for I in M'Range (1) loop
         for J in M'Range (2) loop
            M (I, J) := Count;
            Count := Count + 1;
         end loop;
      end loop;
   end Mk_Matrix;

   procedure M_Mult (M1, M2 : Int_Matrix; MM : in out Int_Matrix) is
      Sum : Int;
   begin
      for I in M1'Range (1) loop
         for J in M2'Range (2) loop
            Sum := 0;
            for KK in M1'Range (2) loop
               Sum := Sum + M1 (I, KK) * M2 (KK, J);
            end loop;
            MM (I, J) := Sum;
         end loop;
      end loop;
   end M_Mult;

   M1, M2, MM : Int_Matrix;
   N          : Positive := 1;

begin
   begin
      N := Positive'Value (Ada.Command_Line.Argument (1));
   exception
      when Constraint_Error => null;
   end;

   Mk_Matrix (M1);
   Mk_Matrix (M2);

   for Iter in 1 .. N loop
      M_Mult (M1, M2, MM);
   end loop;

   Text_IO.Put_Line
     (L_Trim (Int'Image (MM (1, 1))) & Int'Image (MM (3, 4)) &
        Int'Image (MM (4, 3)) & Int'Image (MM (5, 5)));
end Matrix;
