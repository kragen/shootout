\ -*- mode: forth -*-
\ $Id: except.gforth,v 1.1 2004-05-25 02:53:22 bfulgham Exp $
\ http://www.bagley.org/~doug/shootout/
\ from Anton Ertl

\ read NUM from last command line argument
0. argc @ 1- arg >number 2drop drop constant NUM

1 constant *hi*
2 constant *lo*

variable lo
variable hi

: blowup ( n -- )
    1 and if
        *lo* throw
    else
        *hi* throw
    endif ;

: lo-function ( n1 -- n2 )
    ['] blowup catch           ( n2 ball )
    dup *lo* <> tuck and throw ( n2 f )
    1+ lo +! ;

: hi-function ( n1 -- n2 )
    ['] lo-function catch      ( n2 ball )
    dup *hi* <> tuck and throw ( n2 f )
    1+ hi +! ;

: some-function ( n1 -- n2 )
    ['] hi-function catch abort" We shouldn't get here" ;

: main ( -- )
    NUM 0 ?do
        i some-function drop
        loop
    ." Exceptions: HI=" hi ? ." / LO=" lo @ 1 u.r cr ;

main bye
