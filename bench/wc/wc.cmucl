;;; -*- mode: lisp -*-
;;; $Id: wc.cmucl,v 1.5 2005-05-15 06:34:27 bfulgham Exp $
;;; http://shootout.alioth.debian.org/
;;; from Bulent Murtezaoglu (with some code from Andrew McDowell)
;;; Multi-lisp modifications by Brent Fulgham
;;; Optimizations by Patrick Frankenberger

(defun main ()
  (let* ((start 0)
	 (current #\X)			;junk char save me the locally
	 (end 0)
	 (nc 0)
	 (buffer (make-string 4096)))
    (declare (type string buffer) (fixnum start end nc)
	     (base-char current))
    (labels
	((get-char ()
		   (when (= start end)
		     (setf start 0)
		     (setf end (read-sequence buffer *standard-input*))
		     (incf nc end)
		     (when (zerop end)
		       (return-from get-char nil)))
		   (setf current (schar buffer start))
		   (incf start)))
      (let ((nl 0)
	    (nw 0)
	    (inword nil))
	(declare (fixnum nl nw))
	(loop while (get-char) do
	  (cond ((char= current #\newline)
		 (incf nl)
		 (setq inword nil))
		((or (char= current #\space) (char= current #\tab))
		 (setq inword nil))
		((not inword) ;; only tested if we have a word constituent
		 (incf nw) (setq inword t))))
	(format t "~A ~A ~A~%" nl nw nc)))))
